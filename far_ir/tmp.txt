#include <Wire.h>

const int  PIN_NUM = 9;

int far_ir_pin[PIN_NUM] = {4, 5, 6, 7, 8, 9, 10, 11, 12};
int pin_value[PIN_NUM] = {0};

int find_max_ir() {
    int max_value = 0;
    int max_pin = 0;
    for(int i=0; i<PIN_NUM; ++i) {
        if(pin_value[i] > max_value) {
            max_value = pin_value[i];
            max_pin = i; // return value is actual pin number - 2
        }
    }
    return max_pin;
}


//memo status
// 0:,300)
// 1:[300,350)
// 2:[350,400)
// 3:[400,450)
// 4:[450,500)
// 5:[500,540)
// 6:[540,
void request_event() {
    int max_ir[2] = {};
    max_ir[0] = find_max_ir();
    byte send_value[2] = {};
    if(max_ir[0] == 0) {
      if(pin_value[1] > pin_value[2]) {
        max_ir[1] = 1;
      } else {
        max_ir[1] = 2;
      }
    } else if(max_ir[0] == 1) {
      if(pin_value[0] > pin_value[3]) {
        max_ir[1] = 0;
      } else {
        max_ir[1] = 3;
      }
    } else if(max_ir[0] == 7) {
      if(pin_value[5] > pin_value[8]) {
        max_ir[1] = 5;
      } else {
        max_ir[1] = 8;
      }
    } else if(max_ir[0] == 8) {
      if(pin_value[6] > pin_value[7]) {
        max_ir[1] = 6;
      } else {
        max_ir[1] = 7;
      }
    } else {
      if(pin_value[max_ir[0]-2] > pin_value[max_ir[0]+2]) {
        max_ir[1] = max_ir[0]-2;
      } else {
        max_ir[1] = max_ir[0]+2;
      }
    }

    for(int i=0; i<2; ++i) {
        send_value[i] = max_ir[i]*10;
        if(pin_value[max_ir[i]] > 540) {
            send_value[i] += 6;
        } else if(pin_value[max_ir[i]] > 500) {
            send_value[i] += 5;
        } else if(pin_value[max_ir[i]] > 450) {
            send_value[i] += 4;
        } else if(pin_value[max_ir[i]] > 400) {
            send_value[i] += 3;
        } else if(pin_value[max_ir[i]] > 350) {
            send_value[i] += 2;
        } else if(pin_value[max_ir[i]] > 300) {
            send_value[i] += 1;
        }
    }
    //Serial.print(send_value[0]); Serial.print(" ");
    //Serial.println(send_value[1]);
    Wire.write(send_value, 2);
}

void setup() {
    Serial.begin(9600);
    for(int i=0; i<PIN_NUM; ++i) {
        pinMode(far_ir_pin[i],INPUT);
    }
    Wire.begin(0x10);
    Wire.onRequest(request_event);
}

void loop() {
    delay(20);
    for(int i=0; i<PIN_NUM; ++i) {
        pin_value[i] = pulseIn(far_ir_pin[i], LOW, 1500);
        Serial.print(pin_value[i]); 
        Serial.print(" ");
    }
    Serial.println();
}